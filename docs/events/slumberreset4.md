# Untitled (Main)

[Back to event list](./../)

This is generated automatically through code analysis and may include mistakes. For the interactive version of this tool, which looks at your latest savegame to hint the next see the [walkthrough tool here](https://github.com/largestack/Lessons-In-Love-Guide-Tool/blob/main/README.md).


Part of event chain [December 28, 2020](./slumberreset3.md)

## Event preconditions

No event conditions found, it is likely part of an event chain.

## Next events

None

## Event properties

* Id: slumberreset4
* Group: Main
* Triggered by label: slumberreset3
* Chain sources: slumberreset3
* Chain sources path: slumberreset3

## Official wiki page

[Untitled](https://lessonsinlove.wiki/index.php?title=Special%3ASearch&search=slumberreset4&go=Go) for more details.

## Event code

File: (install folder)\game\chap3.rpy

Code:
```python
...
label slumberreset4:
    stop sound
    scene smilecam1
    $ renpy.pause(4, hard=True)
    scene smilecam2
    $ renpy.pause(4, hard=True)
    play sound "static.mp3"
    scene thisisgreen5 with flash
    scene wemadeit1 with flash
    stop sound
    play sound "likepigstopigwater.wav"
    $ renpy.pause(5, hard=True)
    play music "happyhappysmile.mp3"
    $ renpy.pause(15, hard=True)
    play sound "alert.mp3"
    scene wemadeit2

    if cheater == True:
        $ chika_love = 0
        $ yumi_love = 0
        $ sana_love = 0
        $ ayane_love = 0
        $ miku_love = 0
        $ makoto_love = 0
        $ rin_love = 0
        $ futaba_love = 0
        $ maya_love = 0
        $ ami_love = 1000000
        $ molly_love = 0
        $ tsuneyo_love = 0
        $ uta_love = 0
        $ io_love = 0
        $ otoha_love = 0
        $ nodoka_love = 0
        $ touka_love = 0
        $ yasu_love = 0
        $ kirin_love = 0
        $ noriko_love = 0
        $ sara_love = 0
        $ imani_love = 0
        $ maki_love = 0
        $ haruka_love = 0
        $ yuki_love = 0
        $ tsubasa_love = 0
        $ tsukasa_love = 0
        $ chinami_love = 0
        $ kaori_love = 0
        $ karin_love = 0
        $ osako_love = 0
        $ wakana_love = 0
        $ punished = True

    sev "I am here."
    sev "Thank you for taking the time away from your party to join us for a very special episode of “Untitled Children’s Show.”"
    sev "The date is December 28, 2020. The weather exists. And I have yet to scrub your semen out of my roach-holder."

    scene wemadeit3
    play sound "sponsor.mp3"
    $ renpy.pause(7, hard=True)
    scene wemadeit2

    "{i}Welcome to Untitled Children’s Show — the program where we make it more apparent that your interests are unsustainable and that you will one day break the world because of it.{/i}"
    "{i}If it appears that the world is broken now, it is likely due to static interference. We can assure you that the world is perfectly functional.{/i}"
    "{i}Please note that taking it upon yourself to research information that may contradict that is strictly prohibited.{/i}"
    "{i}We take trust very seriously at Untitled Television Studio — and we ask that you maintain the same level of respect that we have maintained with you for [totaldays] days.{/i}"
    "{i}If you don’t, we can not guarantee your continued safety.{/i}"
    "{i}This is your final warning. You must take something {s}ELSE{/s} that does not belong to you before the summer ends.{/i}"
    "{i}Failure to do so will result in an immediate and untimely cancellation.{/i}"

    scene wemadeit4
    with pixellate

    "Are you (or is someone you know) suffering from depression?"
    "Have you tried not having it?"
    "If you have tried not having and still have it, Untitled Children’s Show is looking for participants for an upcoming medical trial."
    "The cure is only two pumps away."
    "Wet roaches (with container) will are being provided to the you participates."
    "Donate today for first pick of fuck hole."

    scene wemadeit2
    with pixellate

    sev "Now then, with all of that out of the way, let me tell you a little about what we’re doing here."
    sev "Please forgive me if I seem nervous or make a mistake. This segment is typically Moyo’s responsibility but...unforeseen circumstances have led to an unfortunate change in her constitution."
    sev "But never fear. Our medical staff is currently in the process of cutting her open to find out what is growing inside. Once that has been discerned, your normal host will return."
    sev "Probably."
    s "..."
    sev "What is the matter?"
    sev "Does the stray cat possess your mouth muscle?"

    play sound "static.mp3"
    scene wemadeit5 with flash
    stop sound

    sev "Or is the sticky comfort slip we provided preventing unnecessary words from dribbling out of your secondary sex hole? Please advise."
    a1 "The words! They will not come to him! Like bugs inside a boat!"
    a2 "We’ve maintained the skill to rhyme sometimes, but still don’t own a coat!"
    a1 "Poems are fun! Appeal is learned! Flower prose and dirty purpose!"
    a2 "Only so much time is left until we see beneath the surface!"
    a1 "Does surface rhyme with purpose????"
    a2 "A diagonal rhyme! Preapproved!"
    a1 "Sinner’s talk! Your coat will never arrive!"
    sev "{i}Ahem...{/i}May I proceed?"
    a2 "{s}ssssssssssssssssssssssssss{/s}"
    a1 "{s}kkkkkkkkkkkkkkkkkkkkkkk{/s}"

    play sound "static.mp3"
    scene wemadeit6 with flash
    stop sound

    sev "As I was saying before our writers decided to intervene — Moyo’s dissection is temporary, but {i}friendship{/i} is forever."
    sev "So today, we will use our time to see just how much you value those you keep close to you."
    sev "I understand that you may feel slightly intimidated in such a weak position, but I can assure you that this is what needed to be done in order to maintain my safety after you raped me and ruined my dress."
    s "{b}{size=+25}I DIDN’T DO IT{/b}{/size}"
    sev "Angels 1 and 2, please remove his comfort slip so he may respond to my inquiries."
    a1 "Understood! We’ll kill the tape!"
    a2 "All he’s ever {i}done{/i} is rape!"

    "My comfort slip is ripped off of my mouth and, along with it, some of my skin departs its home."
    "I’m not too upset as all children eventually grow up and leave for college to start having sex with things, I was just hoping for a little more time with my soft, protective outer flesh barrier."

    s "Aaaaahhhhhh."
    s "Aaahhhh."
    s "AAAAAHHHHHH."

    "Normal words don’t happen. Only scream."

    sev "Now, now. Everything will be okay. In fact, this will likely be your easiest trial yet as it hinges entirely on information that you went out of your way to obtain."
    s "But I paid so much attention to the coffee monologue."
    sev "{i}You{/i} didn’t do anything. But I’m afraid I wouldn’t know what you mean even if you did."

    play sound "static.mp3"
    scene wemadeit7 with flash
    stop sound

    s "Don’t understand. Can’t brain good right now because dream. Are girl okay? Safe? What happen."
    sev "Everyone is fine, don’t worry."
    sev "Well, I guess that more or less hinges on what your personal definition of “fine” is, but it’s not like their roach-holders are being sewn up. You’ll still be able to do sex to them in your free time."
    s "Like other things now too. Sex is bonus level! Am a good guy! Promise!"
    sev "You? Good?"
    sev "TD, cue laugh track."

    play sound "laugh2.mp3"

    s "No laugh! Serious! Trust is important! Italian narrator said so!"
    sev "{i}Italic{/i} narrator says a lot of things, don’t they?"
    s "Untie hands! Itchy! Have to scratch! Have to find roof!"
    sev "If you’re in that much of a hurry, just answer the next twelve questions correctly. If you do that, you can leave."
    sev "Your friendly maggot might not be around to help you this time, but we have taken several precautions to make sure this interview won’t be as painstakingly...painful as the last."

    scene wemadeit8
    with dissolve

    s "No more magazines?"
    sev "No more magazines."
    sev "Just love!"
    sev "Now, please welcome our very special guest..."

    play sound "static.mp3"
    scene wemadeit9 with flash
    stop sound

    sev "The girl whose name you’ve yet to choose!"
    s "Don’t know girl! Can’t choose! Untie rope!"
    q "This..."
    q "This isn’t right..."
    q "This isn’t right!"
    sev "Is there a problem?"

    scene wemadeit10
    with dissolve

    q "A {i}problem?!{/i} Are you kidding me?!"
    q "Look at the shape he’s in! This...this isn’t how it..."
    q "I didn’t agree to this! You’ve gone way too far! Hasn’t he been through enough already?!"
    sev "Some would say he’s been through nothing at all."
    sev "{i}Some{/i} would say the worst has yet to come."
    sev "Now, do we need to remove you as well? Or will you do what you’ve been asked to?"

    scene wemadeit11
    with dissolve

    q "Who...do you think you are?"
    q "What I’ve been {i}asked{/i} to do?..."
    q "You don’t get it at all. You have no idea what’s-"
    sev "Listen, if you want the showrunners to file a complaint about you, I’d be more than happy to-"

    play sound "static.mp3"
    scene wemadeit12 with flash
    stop sound

    y "H...Hi! Yumi...Yamaguchi here! Friendly neighborhood tsundere! Hahaha!"
    s "Yumi! Help rope! Gotta scratch!"
    s "Then, roof together! You remember!"

    play sound "static.mp3"
    scene wemadeit13 with flash
    stop sound

    sev "The way this game will work is simple-"
    sev "I will read you questions about all of the girls at your slumber party, and you will answer honestly and to the best of your ability. I just hope that the “best of your ability” winds up being the truth."
    sev "Because if it isn’t, you’ll be sent back to square one and have to do {i}all{/i} of this over again. And who knows {i}when{/i} the next time you’ll make it this far is?"
    sev "If you’re able to answer all of the questions correctly, though...you’ll be deemed worthy of continuing on and wake up back at home! Entirely sentient and entirely...naked."
    sev "Are you ready to begin?"
    s "Gotta...scratch!"

    scene wemadeit14
    with dissolve

    sev "Fine, fine. But don’t say I never do anything for you."

    play sound "static.mp3"
    scene wemadeit15 with flash
    stop sound
    jump yumigstrivia

label yumigswrong:
    sev "Fact checker?"

    scene wemadeit18
    with dissolve

    y "That’s...I’m sorry, but that’s not right."
    sev "Ooh! How unfortunate! Looks like you’ll just have to start the whole slumber party over!"
    s "The whole thing?! At least magazine event saved progress!"
    sev "It did! But “magazine event” quizzed you on mostly trivial nonsense that our employer pulled out of a hat."
    sev "This time, it’s different. This time you, you didn’t know something important."
    sev "Do you {i}actually{/i} care? Or are you still just pretending to?"
    s "Don’t wanna go back! Too many words! Already know all the coffee!"
    sev "Ahh, if only this was a coffee-related quiz and not one that required a more {i}prolonged{/i} retention of information."

    scene theend
    with dissolve2

    sev "Thanks for joining us on Untitled Children’s Show!"
    sev "Maybe next time, our protagonist won’t make any mistakes!"
    s "Noooooooo!"

    play sound "static.mp3"
    scene frown with flash
    scene black with flash
    stop sound

    jump slumberreset3

label yumigstrivia:
    sev "Question one..."
    sev "Everybody knows I’m a bit of a badass, and it’s not all talk either!"
    sev "I’ve got real experience in both violence {i}and{/i} being neglected, and that goes way back to before I ever even started masturbating to you!"
    s "Yumi wouldn’t! Hates me!"
    sev "Fact checker, can you confirm or deny what the card says? Have I been fed incorrect information?"

    scene wemadeit16
    with dissolve

    y "Uh..."
    y "No..."
    y "The card is right..."
    sev "Of course it is."
    sev "Anyway, that experience has manifested in a number of ways — many of which involve people being hurt."
    sev "So...{i}where{/i} did I start hurting people? Where did my first knockout take place?"
    s "Uhhhhh..."

    $ yumigs1 = renpy.input("The answer is...")
    $ yumigs1 = yumigs1.strip()

    if not yumigs1.lower() in ["daycare", "day care"]:
        jump yumigswrong
    else:
        s "Daycare! Daycare knockout! Told me recently! Remember that!"
        sev "Fact checker?"

        scene wemadeit17
        with dissolve

        y "That’s correct."
        sev "Are you just saying that because you have a soft spot for him?"
        y "No...it’s genuinely correct."
        y "I made a promise to be truthful and I intend to keep it."
        sev "Ah, and {i}speaking{/i} of promises!"
        sev "Question two...it’s been a long time since {i}I’ve{/i} made a promise with anyone...but it’s not because I don’t want to. It’s because I’m scared."
        sev "It’s no secret that my trust issues run deeper than the Mariana Trench, but they only exist in the first place because my mother never brought me...where?"

        $ yumigs2 = renpy.input("The answer is...")
        $ yumigs2 = yumigs2.strip()

        if not yumigs2.lower() in ["okinawa", "tokashiku beach"]:
            jump yumigswrong
        else:
            s "Okinawa! Turtle place!"
            s "Yumi likes turtles! Very cute!"
            sev "Fact checker?"

            scene wemadeit19
            with dissolve

            y "Yeah...he got it."
            s "So good at this. Bring it on, Long Maya. Will answer all question!"
            sev "Then I suppose we’ll go with...a-hah. This one seems a little less obvious."
            sev "What is my favorite beverage?"

            scene wemadeit20
            with dissolve

            s "Never learned! Yumi never told! Not good conversation topic!"
            sev "Sometimes, showing is telling. Now go on, take a guess."

            $ yumigs3 = renpy.input("The answer is...")
            $ yumigs3 = yumigs3.strip()

            if not yumigs3.lower() in ["water"]:
                jump yumigswrong
            else:
                s "Water! Yumi drinks all the water! She is so hydrated!"
                sev "Hmmm....fact checker?"

                scene wemadeit21
                with dissolve

                y "Damn right. Though it ain’t as much of an actual “favorite” as it is a thing I just always fuckin’ have on me."
                y "Ain’t like I can really afford anythin’ else, though."

                play sound "static.mp3"
                scene wemadeit22 with flash
                stop sound
                play sound "jackpot.mp3"

                sev "What great news! You’ve managed to pass the Yumi round!"
                sev "But coming up next is a real enigma..."

                jump tsuneyogstrivia

label tsuneyogswrong:
    sev "Fact checker?"

    scene wemadeit24
    with dissolve

    t "No...that’s not right."
    s "Ahhhhhhh!"
    sev "Are you absolutely sure?"
    t "I am."
    sev "{i}Absolutely?{/i}"
    t "I have never been more sure of anything in my life."
    sev "How unfortunate! Because that means our protagonist is going to have to start over at the beginning of the slumber party!"
    s "The beginning?! That is so long ago!"
    sev "It is! But that’s the price you have to pay for not caring and valuing everyone’s time and opinions!"
    sev "We’re only doing this to help you, you know. Like anyone else, we want you to succeed!"
    s "Then let me try again! Will get it right this time!"
    sev "You {i}can{/i} try again!"
    s "Yay!"
    sev "You’ll just have to hold down the CTRL key for a little while. Which you’re probably already familiar with if you’re getting these questions wrong."
    s "AAHHHHH SO MEAN LONG MAYA!"

    scene theend
    with dissolve2

    sev "Thanks for joining us on Untitled Children’s Show!"
    sev "Maybe next time, our protagonist won’t make any mistakes!"
    s "Noooooooo!"

    play sound "static.mp3"
    scene frown with flash
    scene black with flash
    stop sound
    jump slumberreset3

label tsuneyogstrivia:
    play sound "static.mp3"
    scene wemadeit23 with flash
    stop sound

    sev "Tsuneyo Tojo! Pachipachipachipachipachi."
    t "Hello."
    s "Hi Tsuneyo. What was smell."
    t "I’m afraid that is information I do not currently possess, bro."
    s "Oh sorry didn’t know."
    t "Fuck you."
    sev "Question one..."
    sev "My knowledge of the outside world is limited due to extremely minimal exposure over the years, but that doesn’t mean I don’t know anything at all!"
    sev "In fact, I consider myself a bit of a repository when it comes to vital information...and there are very few quotes I can not properly attribute to their respective sources."
    sev "A wise man once said, “There is nothing to fear but fear itself!”"
    sev "Who was that man?"

    $ tsuneyogs1 = renpy.input("The answer is...")
    $ tsuneyogs1 = tsuneyogs1.strip()

    if not tsuneyogs1.lower() in ["franklin delaware roosevelt"]:
        jump tsuneyogswrong
    else:
        sev "Fact checker?"

        scene wemadeit25
        with dissolve

        t "That is correct. Franklin Delaware Roosevelt is the man who originally said that — not to be confused with a man who went by a similar name that was very likely misspelled in the middle."
        sev "I’m not sure if I understand, but there is a great deal I’ll {i}never{/i} understand when it comes to you, Miss Tojo."
        sev "If only there was some sort of window I could peer through that would allow me to see into your soul."
        sev "But speaking of which, our second question is this!"
        sev "How many windows are there in Tojo Ramen?"

        $ tsuneyogs2 = renpy.input("The answer is...")
        $ tsuneyogs2 = tsuneyogs2.strip()

        if not tsuneyogs2.lower() in ["zero", "0"]:
            jump tsuneyogswrong
        else:
            s "Trick question! No windows in Tojo Ramen! Very dark restaurant!"
            sev "Is this true, Tsuneyo?"

            scene wemadeit26
            with dissolve

            t "It is. Though I am unable to confirm that things will stay that way forever as I am still contemplating investing in a window display."
            sev "There are no windows on the second floor either?"
            t "Why are you asking me questions about the second floor?"
            sev "I’m just looking for confirmation."
            t "I don’t believe you and am not willing to speak about this any further. Please ask the third question. You are wasting everyone’s time."
            sev "Fine. No one was looking forward to having you on this segment anyway."
            t "Say that again, you bastard."
            sev "Question three..."
            sev "What is the most important dish we serve at Tojo Ramen?"
            t "Easy. The answer to that question is “ramen.” Please move on to question four now."
            sev "The questions aren’t for you and I’d really appreciate it if you’d be able to step out of character while I’m reading the cards."
            sev "Also, the answer you gave isn't correct as it needs to match the card {i}exactly.{/i}"
            t "Impossible. I answered the question and it is time to move on."
            sev "Do {i}you{/i} have a different answer?"

            $ tsuneyogs3 = renpy.input("The answer is...")
            $ tsuneyogs3 = tsuneyogs3.strip()

            if not tsuneyogs3.lower() in ["tori shio", "tori shio ramen"]:
                jump tsuneyogswrong
            else:
                s "Tori shio ramen! So good! Have it on a card!"

                sev "Fact checker?"

                scene wemadeit27
                with dissolve

                t "Tch."
                sev "Wow, what’s that reaction for?"
                sev "Could it be there is a little more to that dish than you’re letting on?"
                t "Of course not. Noodles are noodles. They are all equally important."
                t "I am reacting this way as I am disappointed in myself for forgetting the answer. There is nothing more to it."

                play sound "static.mp3"
                scene wemadeit22 with flash
                stop sound
                play sound "jackpot.mp3"

                sev "Well, whatever the case, it looks like the Tsuneyo Tojo round is now complete!"
                sev "Congratulations on making it halfway through the segment and reaching the first and only checkpoint!"
                sev "It is at this point that we’d like to say thank you to our sponsors in the form of a quick commercial break! But not before asking one more {i}bonus{/i} question..."
                sev "You’ll only have one chance to answer this, so think {i}very{/i} carefully..."
                sev "Below the egg and dancing three, a picture book sits peacefully."
                sev "You'd hide something beneath your bed...do you remember what the caption said?"

                $ gsbonus = renpy.input("The answer is...")
                $ gsbonus = gsbonus.strip()

                if not gsbonus.lower() in ["cute puppy"]:
                    stop music
                    scene youidiot
                    play music "cafe.mp3"
                    $ renpy.pause(15, hard=True)
                    $ mothersmiss = True
                    jump gstriviaround2
                else:
                    jump mothersmilk

label mayagswrong:
    sev "Fact checker, is that correct?"

    scene wemadeit29
    with dissolve

    m "No...it’s not."
    sev "Oooh, too bad! Thankfully, you did manage to reach the checkpoint after completing Miss Tojo’s round. So at least you won’t have to go {i}too{/i}far back."
    s "Don’t wanna go back at all! Hurts my head! Wanna keep going!"
    sev "Unfortunately, continuing on {i}without{/i} reverting to an older state may result in some...unfortunate side effects for the girls in question."
    sev "They’re all directly connected to what’s going on right now, and every incorrect answer you provide actually inflicts extreme harm on them in their own personal trials!"
    s "Don’t wanna hurt! Wanna hug!"
    sev "Then be a {b}GOOD BOY{/b} and start over, okay?"

    scene theend
    with dissolve2

    sev "Thanks for joining us on Untitled Children’s Show!"
    sev "Maybe next time, our protagonist won’t make any mistakes!"
    s "Noooooooo!"

    play sound "static.mp3"
    scene frown with flash
    scene black with flash
    stop sound
    jump gstriviaround2

label gstriviaround2:
    play sound "static.mp3"
    scene wemadeit13 with flash
    stop sound
    play music "happyhappysmile.mp3"

    sev "Welcome back to Untitled Children’s Show. I’m your stand-in host, 〄."
    sev "If you’re just joining us now, our contestant has made it halfway through his trial without managing to ruin everything, like he so commonly does."
    sev "Will he make it through the next two rounds as well? Text your votes to the letter X and to let us know what you, the viewer, think!"

    play sound "static.mp3"
    scene wemadeit28 with flash
    stop sound

    sev "Round three begins with none other than {b}HER.{/b}"
    m "Can we make this quick? I’d really prefer not almost sleeping through the apocalypse again and I’ve been noticing a disturbing pattern throughout the last several resets."
    sev "Our first question about {b}HER{/b} includes a character detail that has seemed mostly unimportant up to this point in the story, but may one day help you remember something!"
    sev "{b}SHE{/b} plays an instrument. What is it?"

    $ mayags1 = renpy.input("The answer is...")
    $ mayags1 = mayags1.strip()

    if not mayags1.lower() in ["violin"]:
        jump mayagswrong
    else:
        s "Violin! Has one on a box but never touches it! Always wonder why!"
        sev "Fact checker?"

        scene wemadeit30
        with dissolve

        m "That is correct. But the reason I no longer play it is of no concern to you right now."
        s "That’s okay. Will wait for emotional violin scene. Gonna happen."
        m "Let’s focus on getting out of here before thinking about what the future may hold...because it’s quite possible there will {i}be{/i} no future if we remain trapped in here."
        sev "Saying you’re “trapped” isn’t all that accurate as you’d be here regardless of the program, but we can look past that right now for the sake of brevity."
        s "Ask me favorite food! Know that one! Won’t get wrong!"
        sev "I’m afraid that one would be a bit too easy. What I’m looking for is...ah. Found it."
        sev "I’ve always been a girl of many faces...and for every common-knowledge hobby of mine, like my love for manga or tendency to {i}also{/i} masturbate while thinking of you-"

        scene wemadeit31
        with dissolve

        m "Is it really necessary to keep bringing that-"
        sev "True or false, fact checker?"
        m "I don’t have to-"
        sev "True? Or false?"
        m "..."

        scene wemadeit32
        with dissolve

        m "True...even though it pains me to admit it."
        sev "There’s nothing to be ashamed of. I often fill my sex crevice with pens and pretend that they are all tinier, thinner versions of him. I’m sure you do the same."
        m "I don’t."
        sev "Fact checker, please fact check yourself."
        m "False. Maya doesn’t- {i}I{/i} don’t do that."
        sev "You don’t do that {i}yet...{/i}but I digress."
        sev "In addition to my wealth of hobbies, there is something I collect. What is it?"

        $ mayags2 = renpy.input("The answer is...")
        $ mayags2 = mayags2.strip()

        if not mayags2.lower() in ["masks"]:
            jump mayagswrong
        else:
            s "Masks! She is a mask girl! See those in her room too! On the wall!"
            sev "Fact che-"
            m "Correct. Next question."
            sev "Wow. {i}Someone{/i} is in a hurry, aren’t they?"
            m "As I said, I want to get out of here. I can’t do that if you’re going to keep stalling and asking unrelated personal questions."
            m "I don’t do this to take advantage of people’s memories, you know. I do it because-"
            sev "It doesn’t matter {i}why{/i} you do it, just that you {i}do.{/i}"
            sev "I’ll go ahead and ask the next question, but it’s not because I sympathize with you. It’s because we’re running out of time and there is a new soap opera scheduled after this."
            sev "I should also check on Moyo in the operating room. The doctors don’t have as much experience with the inside of her uterus as I do."
            m "..."
            sev "{i}Ahem...{/i}Question three..."
            sev "While I may be conceited, there will always be...{i}limits{/i} to that. Just like any other girl, or any other {i}person,{/i} I have things that I can’t help but feel insecure about sometimes."
            sev "You’re constantly surrounded by girls...and all of them feel at times like {i}they{/i} are the front runner for your heart."
            sev "But that should be {i}me.{/i} I’m the one you’re supposed to love, not {i}them.{/i}"
            sev "But even with that being said...there is one other person I can’t help but feel jealous of. Even {i}if{/i} I think I am superior to them in basically every single way."
            sev "Who is that person?"

            $ mayags3 = renpy.input("The answer is...")
            $ mayags3 = mayags3.strip()

            if not mayags3.lower() in ["niki", "niki nakayama"]:
                jump mayagswrong
            else:
                s "Hard think. Brain is soup."
                s "Is it...Niki?"
                sev "Fact checker?"

                scene wemadeit33
                with dissolve

                m "..."
                s "..."
                sev "{i}Ahem...{/i}"
                m "That’s correct."
                m "I wasn’t your first love...{i}or{/i} your second, but..."
                m "..."

                jump gscompletehooray

label gscompletehooray:
    scene wemadeit34
    with dissolve

    m "I can’t do this anymore. I have to bail out."
    m "It’s one thing getting inside the head of Yumi or Tsuneyo, but do you have any idea just how much is going on in this one? I can barely make sense of any of it."
    sev "You can’t leave yet. We still have one more round of questions."

    scene wemadeit35
    with dissolve

    m "You’re going to make me do {i}her{/i} too?!"
    sev "Was that not obvious from the beginning? I specifically stated there were twelve questions."
    m "There are stipulations in my contract that {i}specifically{/i} list her as an exception. I can’t."
    sev "Can’t? Or {i}won’t?{/i}"
    m "Why does it matter?! You’ve got answers on your cards, don’t you? Just read those and go without a fact checker. I can’t do it."
    sev "Okay, but...it would be a real shame if we accidentally damaged him as a result of this."
    m "Damaged him {i}how?{/i} What does that even mean? Haven’t you already done enough?"
    sev "Feeding someone incorrect information can cause more damage than torture at times."
    sev "I’m an unbiased reporter. I just want to make sure he has the facts."
    sev "All of those facts just happen to belong to you."
    m "..."
    sev "How about this...we’re already running over on time, so why don't we cut two of the questions out and finish after just one more?"

    play sound "static.mp3"
    scene wemadeit36 with flash
    stop sound

    q "Just one?"
    sev "Just one."
    q "What is it?"
    sev "I choose...this one."
    q "What is it? Tell me. And I’m not answering anything gross anymore, so you can forget it if it’s something like that."
    sev "I can’t read the question until you change...but what I {i}can{/i} tell you..."
    sev "Is it may very well change the future."

    scene wemadeit37
    with dissolve2

    q "..."
    sev "..."
    q "You’re sick."
    q "You’re {i}all{/i} sick. I don’t understand how you can do this to someone so...helpless. He’s broken. Can’t you see that?"
    sev "He can be fixed."
    sev "They can always be fixed."
    q "I’m done after this. I’m not coming back."
    sev "Moyo will be very upset to hear that. She likes you very much."

    play sound "static.mp3"
    scene wemadeit38
    with flash
    stop sound
    stop music

    sev "Final question..."
    sev "You got me pregnant, but I never told you about it because I am a coward."
    sev "Something happened that made the baby go away."
    sev "I remember, but I pretend that I don’t because it makes it hurt less."
    sev "I picked out a name for her."
    sev "It was one I knew you’d like."
    sev "What was it?"

    $ whatyousaid = renpy.input("The answer is...")
    $ whatyousaid = whatyousaid.strip()

    s "[whatyousaid]."
    ay "..."
    s "..."
    sev "Fact checker?"
    ay "..."
    s "..."
    sev "I’m going to need an answer."
    ay "I..."
    ay "I can’t give you one."
    sev "You can’t? How strange. Why do you think that is?"
    ay "..."
    sev "Perhaps...you’re afraid of what will happen if you do?"
    ay "..."
    sev "Of what will happen to {i}you.{/i}"
    ay "I’m sorry."
    ay "I can’t protect you."

    play sound "eggcrack.mp3"
    scene wemadeit39 with hpunch

    sev "Oooh, too bad! It looks like we’ll have to send you back to the beginning of everything after all!"
    sev "I’ll remember you always, [mayamaster]. The feeling of your cock still lingers in my-"

    play sound "static.mp3"
    scene wemadeit40 with flash
    stop sound
    play music "alarmbell.mp3"

    sev "What? What happened?"
    s "..."
    sev "{i}Now? But I was informed that wasn’t possible.{/i}"
    s "..."
    sev "Does It know yet?"
    s "..."
    sev "I see..."

    if cheater == True:
        sev "..."
        sev "Yes. Adjustments have already been made and precautionary measures have been taken to ensure it does not happen again."
        sev "..."
        sev "Understood."

    play sound "static.mp3"
    scene wemadeit41 with flash
    stop sound

    sev "It appears that there has been an interruption in today’s broadcast."
    sev "If your connection has not yet been severed and you are still watching this program, thank you. But it is with a heavy heart that I must sign off."
    sev "Thank you again to our sponsors and to all of the people who-"

    scene wemadeit42
    play sound "pop.mp3"

    sev "Oh no."
    sev "He popped."

    $ renpy.end_replay()
    $ slumberreset4 = True
    stop music
    jump slumberreset5

label slumberreset5:
...
```

## Code that triggers this event

File: (install folder)\game\chap3.rpy

Code:
```python
...
"It’s worth mentioning that the learning curve is more forgiving than the Hario V60 or the Kalita Wave, meaning it’s a great way to get your feet wet in the world of pour over coffee. "
    "Tsuneyo sure is taking a long time out there."
    "The Clever Dripper looks like just any other pour over dripper, but on closer inspection you’ll notice that the Clever Dripper is a cross between a steeping and pour over brewer. "
    "What sets this lil’ dude apart from the rest of the dripper family is its clever little valve that stops your brew draining into your cup/vessel until you activate it. "
    "If you haven’t heard of cold drip coffee you must be a fucking idiot. That or you’ve been hiding under a rock for the past few years. "
    "Cold brew is one of the most popular caffeine infused innovations of our time – and no, we are not talking about iced coffee. "
    "In a nutshell, it’s made by slowly dripping cold filtered water through your fresh grinds for a long period – often 10 hours or more. Enough time to kidnap someone or repair a section of a broken fence."
    "Derived from cold brew coffee, nitro coffee is the newest kid on the block in the coffee world, and you can expect to hear a lot more about it in the future."
    "As the name suggests, its’ (cold brewed) coffee, pumped full of nitrogen, which affects the taste and the texture in a very, very nice way. "
    "The result is similar to a cold brew coffee but crisper, a little sweeter and it looks like a pint of Guinness — a beverage that does not exist in Kumon-mi since we are blocked off from the rest of the world."
    "Aren’t you excited to know so much about coffee now?"
    "Let's learn some more."
    "Cowboy coffee is the oldest known method of brewing coffee."
    "It’s outdated, but it works, and you don’t need much to make it happen. "
    "Commonly used around campsites where nobody has bothered to pack any coffee making gear, all you’ll need is a flame and a saucepan. "
    "In a nutshell: fill your pot with water, bring it to a boil, throw in your ground coffee, remove it from the heat and let it brew for a few minutes. "
    "Once the grounds settle to the bottom of the pot, you can pour your coffee into your mug, slowly and gently. Nothing fancy required. "
    "From 1299 The Ottoman Empire ruled Turkey for an impressively long stint, and strong Turkish coffee played a significant role in fueling their endurance. "
    "There’s a very good chance that I made up that last fact, however, you can be sure that (A) Turkish coffee packs a punch, and (B) It’s been enjoyed around the world for a very long time."
    "Brewing Turkish coffee seems easy, but with like most brew methods, there’s skill in doing it right. Learn the skills by reading a guide or something."
    "The most common way involves a Turkish coffee pot, water and very finely ground coffee beans. "
    "You’ll simmer the brew a number of times (2-3 times) and end up with a brew that you’ll love or hate: strong but exceptional tasting with a thick foam on top. "
    "Congratulations. Now you are the coffee expert."

    play sound "knock.mp3"
    scene resetslumber16

    s "I’ll get it."

    play sound "static.mp3"
    scene resetslumber17 with flash
    stop sound

    s "Hey. How was your phone call?"
    t "It was fine. Apologies for locking myself out of the room. I am not used to doors that do that automatically. "
    s "It’s fine. I boiled some water for you if you want to make your own tea. I solemnly swear that I didn’t drug it either."
    t "I thank you for taking the time to both boil the water and not place any drugs in it, but I regret to inform you that I will no longer be able to stay here tonight."
    s "What? Why not?"
    t "A neighbor has reported a strange smell coming from Tojo Ramen. I must return home to investigate this and make sure that everything is in order."
    t "It is highly probable that the power has gone out once again and that food is beginning to spoil."
    t "However, if it is something as serious as a gas leak, please be prepared to never see me again as I may explode."
    s "What about your father? If there’s a gas leak, shouldn’t he be the number-one priority?"
    t "I am worried about him, yes. Which is precisely why I must return."
    t "I apologize for the inconvenience. But if the world does not end and you reconvene tomorrow, I’d be happy to join you once more."
    s "That’s fine since it’s an emergency and everything...the others are just going to be a little disappointed since they had to pull a bunch of strings to make sure this happened."

    scene resetslumber18
    with dissolve

    t "Is that so?"
    s "Yeah...keeping Ami away from us isn’t really an easy task, you know."
    t "I see..."
    s "Oh well. Get going, then. I’ll be the one to break the bad news. Wouldn’t want {i}you{/i} having to upset them and I’m already pretty much used to it."
    t "I’m sure they will be fine. "
    t "They seem to be having a great deal of fun right now."

    play sound "static.mp3"
    scene resetslumber19 with flash
    stop sound
    stop music

    s "Yeah, they’re getting along much-"

    play sound "dooropen.mp3"

    t "Please excuse me. "
    t "Thank you again for the invitation."
    s "........."
    s "........"
    s "......."
    s "......"
    s "....."
    s "...."
    s "..."
    s ".."
    s "."
    s " "

    "I’m fine."
    "..."
    "I’m fine."

    play sound "static.mp3"
    scene resetslumber20 with flash
    stop sound

    six "73 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 68 "
    six "74 68 65 20 65 6e 64 20 6f 66 20 64 61 79 73 20 69 73 20 75 70 6f 6e 20 75 73 "
    six "68 61 76 65 20 79 6f 75 20 66 6f 75 6e 64 20 69 74 20 79 65 74 3f "
    six "74 68 65 20 70 61 72 61 64 6f 78 3f "

    scene spider1
    play sound "happybaby.mp3"
    $ renpy.pause(6, hard=True)
    scene spider2
    $ renpy.pause(6, hard=True)
    $ renpy.end_replay()
    $ slumberreset3 = True

    jump slumberreset4
...
```